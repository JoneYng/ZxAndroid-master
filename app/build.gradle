plugins {
    id 'com.android.application'
}
apply from: "../base.build.gradle"
apply plugin: 'walle'
//多渠道打包插件
//apply plugin: 'reinforce'
apply from: 'reinforce.gradle'

android {

    signingConfigs {
        release {
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASS
            storeFile file(KEY_PATH)
            storePassword KAY_PASS
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    defaultConfig {
        applicationId "com.zx.android"
        multiDexEnabled true

        ndk {
            abiFilters "armeabi-v7a"
        }

        signingConfig signingConfigs.release
        //友盟QQ配置
         manifestPlaceholders = [qqappid: "101375266"]
    }

    //打包改名
    android.applicationVariants.all { variant ->
        def appName = rootProject.name
        def buildType = variant.buildType.name
        def outputTime = new Date().format("yyyy_MMdd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
        def versionName = variant.versionName
        def versionCode = variant.versionCode
        variant.outputs.each { output ->
            if ("release" == buildType) {
                if (output.outputFileName.endsWith('.apk')) {
//                    output.outputFileName = "${versionName}\\${appName}_${versionCode}_V${versionName}_${outputTime}.apk"
                    output.outputFileName = "${appName}_${versionCode}_${outputTime}.apk"
                }
            } else {
                if (output.outputFileName.endsWith('.apk')) {
                    output.outputFileName = "${appName}_${versionCode}_debug.apk"
                }
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        dataBinding = true
        // for view binding :
        // viewBinding = true
    }

}

dependencies {
    implementation project(":lib_base_module")
    if (!isModule.toBoolean()) {
        implementation project(':module_main')
        implementation project(':module_home')
        implementation project(':module_me')
    }
}
configurations.all {
    resolutionStrategy {
        force 'androidx.core:core-ktx:1.6.0'
        force 'androidx.core:core:1.6.0'

        force 'androidx.appcompat:appcompat:1.3.1'
        force "androidx.activity:activity:1.3.1"
        force "androidx.fragment:fragment:1.3.1"
    }
}
